{
  parserClass="com.neueda.jetbrains.plugin.graphdb.language.cypher.CypherParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  elementTypeHolderClass="com.neueda.jetbrains.plugin.graphdb.language.cypher.psi.CypherTypes"
  elementTypeClass="com.neueda.jetbrains.plugin.graphdb.language.cypher.psi.CypherElementType"
  tokenTypeClass="com.neueda.jetbrains.plugin.graphdb.language.cypher.psi.CypherTokenType"

  psiImplUtilClass="com.neueda.jetbrains.plugin.graphdb.language.cypher.psi.CypherPsiImplUtil"
  psiClassPrefix="Cypher"
  psiImplClassSuffix="Impl"
  psiPackage="com.neueda.jetbrains.plugin.graphdb.language.cypher.psi"
  psiImplPackage="com.neueda.jetbrains.plugin.graphdb.language.cypher.psi.impl"

  tokens=[
    // keywords
    K_MATCH="regexp:[Mm][Aa][Tt][Cc][Hh]"
    K_RETURN="regexp:[Rr][Ee][Tt][Uu][Rr][Nn]"
    K_DISTINCT="regexp:[Dd][Ii][Ss][Tt][Ii][Nn][Cc][Tt]"
    K_UNION="regexp:[Uu][Nn][Ii][Oo][Nn]"
    K_ALL="regexp:[Aa][Ll][Ll]"
    K_LOAD="regexp:[Ll][Oo][Aa][Dd]"
    K_CSV="regexp:[Cc][Ss][Vv]"
    K_WITH="regexp:[Ww][Ii][Tt][Hh]"
    K_HEADERS="regexp:[Hh][Ee][Aa][Dd][Ee][Rr][Ss]"
    K_FROM="regexp:[Ff][Rr][Oo][Mm]"
    K_AS="regexp:[Aa][Ss]"
    K_FIELDTERMINATOR="regexp:[Ff][Ii][Ee][Ll][Dd][Tt][Ee][Rr][Mm][Ii][Nn][Aa][Tt][Oo][Rr]"
    K_CREATE="regexp:[Cc][Rr][Ee][Aa][Tt][Ee]"
    K_CONSTRAINT="regexp:[Cc][Oo][Nn][Ss][Tt][Rr][Aa][Ii][Nn][Tt]"
    K_ON="regexp:[Oo][Nn]"
    K_ASSERT="regexp:[Aa][Ss][Ss][Ee][Rr][Tt]"
    K_IS="regexp:[Ii][Ss]"
    K_UNIQUE="regexp:[Uu][Nn][Ii][Qq][Uu][Ee]"
    K_EXISTS="regexp:[Ee][Xx][Ii][Ss][Tt][Ss]"
    K_INDEX="regexp:[Ii][Nn][Dd][Ee][Xx]"
    K_DROP="regexp:[Dd][Rr][Oo][Pp]"
    K_START="regexp:[Ss][Tt][Aa][Rr][Tt]"
    K_WHERE="regexp:[Ww][Hh][Ee][Rr][Ee]"
    K_NODE="regexp:[Nn][Oo][Dd][Ee]"
    K_RELATIONSHIP="regexp:[Rr][Ee][Ll][Aa][Tt][Ii][Oo][Nn][Ss][Hh][Ii][Pp]"
    K_REL="regexp:[Rr][Ee][Ll]"
    K_OPTIONAL="regexp:[Oo][Pp][Tt][Ii][Oo][Nn][Aa][Ll]"
    K_USING="regexp:[Uu][Ss][Ii][Nn][Gg]"
    K_JOIN="regexp:[Jj][Oo][Ii][Nn]"
    K_SCAN="regexp:[Ss][Cc][Aa][Nn]"
    K_SHORTESTPATH="regexp:[Ss][Hh][Oo][Rr][Tt][Ee][Ss][Tt][Pp][Aa][Tt][Hh]"
    K_ALLSHORTESTPATHS="regexp:[Aa][Ll][Ll][Ss][Hh][Oo][Rr][Tt][Ee][Ss][Tt][Pp][Aa][Tt][Hh][Ss]"
    K_UNWIND="regexp:[Uu][Nn][Ww][Ii][Nn][Dd]"
    K_MERGE="regexp:[Mm][Ee][Rr][Gg][Ee]"
    K_SET="regexp:[Ss][Ee][Tt]"
    K_DELETE="regexp:[Dd][Ee][Ll][Ee][Tt][Ee]"
    K_DETACH="regexp:[Dd][Ee][Tt][Aa][Cc][Hh]"
    K_REMOVE="regexp:[Rr][Ee][Mm][Oo][Vv][Ee]"
    K_FOREACH="regexp:[Ff][Oo][Rr][Ee][Aa][Cc][Hh]"
    K_IN="regexp:[Ii][Nn]"
    K_ORDER="regexp:[Oo][Rr][Dd][Ee][Rr]"
    K_BY="regexp:[Bb][Yy]"
    K_DESCENDING="regexp:[Dd][Ee][Ss][Cc][Ee][Nn][Dd][Ii][Nn][Gg]"
    K_DESC="regexp:[Dd][Ee][Ss][Cc]"
    K_ASCENDING="regexp:[Aa][Ss][Cc][Ee][Nn][Dd][Ii][Nn][Gg]"
    K_ASC="regexp:[Aa][Ss][Cc]"
    K_SKIP="regexp:[Ss][Kk][Ii][Pp]"
    K_LIMIT="regexp:[Ll][Ii][Mm][Ii][Tt]"
    K_PERIODIC="regexp:[Pp][Ee][Rr][Ii][Oo][Dd][Ii][Cc]"
    K_BEGIN="regexp:[Bb][Ee][Gg][Ii][Nn]"
    K_COMMIT="regexp:[Cc][Oo][Mm][Mm][Ii][Tt]"
    K_XOR="regexp:[Xx][Oo][Rr]"
    K_OR="regexp:[Oo][Rr]"
    K_AND="regexp:[Aa][Nn][Dd]"
    K_NOT="regexp:[Nn][Oo][Tt]"
    K_STARTS="regexp:[Ss][Tt][Aa][Rr][Tt][Ss]"
    K_ENDS="regexp:[Ee][Nn][Dd][Ss]"
    K_CONTAINS="regexp:[Cc][Oo][Nn][Tt][Aa][Ii][Nn][Ss]"
    K_NULL="regexp:[Nn][Uu][Ll][Ll]"
    K_TRUE="regexp:[Tt][Rr][Uu][Ee]"
    K_FALSE="regexp:[Ff][Aa][Ll][Ss][Ee]"
    K_FILTER="regexp:[Ff][Ii][Ll][Tt][Ee][Rr]"
    K_EXTRACT="regexp:[Ee][Xx][Tt][Rr][Aa][Cc][Tt]"
    K_REDUCE="regexp:[Rr][Ee][Dd][Uu][Cc][Ee]"
    K_ANY="regexp:[Aa][Nn][Yy]"
    K_NONE="regexp:[Nn][Oo][Nn][Ee]"
    K_SINGLE="regexp:[Ss][Ii][Nn][Gg][Ll][Ee]"
    K_CASE="regexp:[Cc][Aa][Ss][Ee]"
    K_ELSE="regexp:[Ee][Ll][Ss][Ee]"
    K_END="regexp:[Ee][Nn][Dd]"
    K_WHEN="regexp:[Ww][Hh][Ee][Nn]"
    K_THEN="regexp:[Tt][Hh][Ee][Nn]"
    K_PROFILE="regexp:[Pp][Rr][Oo][Ff][Ii][Ll][Ee]"
    K_EXPLAIN="regexp:[Ee][Xx][Pp][Ll][Aa][Ii][Nn]"
    K_CYPHER="regexp:[Cc][Yy][Pp][Hh][Ee][Rr]"
    K_CALL="regexp:[Cc][Aa][Ll][Ll]"
    K_YIELD="regexp:[Yy][Ii][Ee][Ll][Dd]"
    K_COUNT="regexp:[Cc][Oo][Uu][Nn][Tt]"
    K_DO="regexp:[Dd][Oo]"
    K_FOR="regexp:[Ff][Oo][Rr]"
    K_REQUIRE="regexp:[Rr][Ee][Qq][Uu][Ii][Rr][Ee]"
    K_MANDATORY="regexp:[Mm][Aa][Nn][Dd][Aa][Tt][Oo][Rr][Yy]"
    K_SCALAR="regexp:[Ss][Cc][Aa][Ll][Aa][Rr]"
    K_OF="regexp:[Oo][Ff]"
    K_ADD="regexp:[Aa][Dd][Dd]"
    K_KEY="regexp:(K|k)(E|e)(Y|y)"

    // literals
    l_identifier="regexp:[a-zA-Z_][a-zA-Z_$0-9]*"
    l_identifier_text="regexp:\`[^`]+\`"
    l_decimal="regexp:(0|[1-9][0-9]*)\.[0-9]+"
    l_integer="regexp:0|[1-9][0-9]*"
    l_string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"

    // base
    semicolon=";"
    parenthese_open="("
    parenthese_close=")"
    bracket_CurlyOpen="{"
    bracket_CurlyClose="}"
    bracket_SquareOpen="["
    bracket_SquareClose="]"
    dollar="$"
    op_colon=":"
    op_dot="."
    op_equal="="
    op_lessThen="<"
    op_greatherThen=">"
    op_plus="+"
    op_minus="-"
    op_mul="*"
    op_bactick="`"
    op_comma=","
    op_questionSign="?"
    op_pipe="|"
    op_range=".."
    op_plusEquals="+="
    op_invalidNotEquals="<>"
    op_notEquals="!="
    op_lessThanEquals="<="
    op_greaterThanEquals=">="
    op_divide="/"
    op_modulo="%"
    op_pow="^"
    op_regexMatch="=~"
  ]
}

cypher ::= StatementItem *

StatementItem ::= !<<eof>> Statement ';'? {pin=1 recoverWhile=statement_recover}
private statement_recover::= !(';'
                        | K_USING
                        | K_UNION
                        | K_LOAD
                        | K_START
                        | K_MATCH
                        | K_UNWIND
                        | K_MERGE
                        | K_CREATE
                        | K_DROP
                        | K_SET
                        | K_DELETE
                        | K_REMOVE
                        | K_FOREACH
                        | K_WITH
                        | K_RETURN
                        | K_BEGIN
                        | K_COMMIT
                        | K_CYPHER
                        | K_PROFILE
                        | K_EXPLAIN
                        | K_CALL
                        | K_OPTIONAL
                        | K_DETACH)

/*************************
 * Statement
 */
Statement ::= (K_BEGIN) | (K_COMMIT) | (QueryOptions? (Command | Query))

/*************************
 * Statement options
 */
QueryOptions ::= AnyCypherOption* ;
AnyCypherOption ::= CypherOption | Explain | Profile;
CypherOption ::= K_CYPHER VersionNumber? ConfigurationOption*
VersionNumber ::= DoubleLiteral
Explain ::= K_EXPLAIN
Profile ::= K_PROFILE
ConfigurationOption ::= SymbolicNameString '=' SymbolicNameString

/*************************
 * Command
 */
Command ::= CreateIndex
          | DropIndex
          | CreateUniqueConstraint
          | DropUniqueConstraint
          | CreateNodePropertyExistenceConstraint
          | DropNodePropertyExistenceConstraint
          | CreateRelationshipPropertyExistenceConstraint
          | DropRelationshipPropertyExistenceConstraint
          | CreateNodeKeyConstraint
          | DropNodeKeyConstraint

CreateIndex ::= K_CREATE IndexSyntax
DropIndex ::= K_DROP IndexSyntax
IndexSyntax ::= K_INDEX K_ON NodeLabel "(" PropertyKeyNames ")"

CreateUniqueConstraint ::= K_CREATE UniqueConstraintSyntax
DropUniqueConstraint ::= K_DROP UniqueConstraintSyntax
UniqueConstraintSyntax ::= K_CONSTRAINT K_ON "(" Variable NodeLabel ")" K_ASSERT PropertyExpression K_IS K_UNIQUE

CreateNodePropertyExistenceConstraint ::= K_CREATE NodePropertyExistenceConstraintSyntax
DropNodePropertyExistenceConstraint ::= K_DROP NodePropertyExistenceConstraintSyntax
NodePropertyExistenceConstraintSyntax ::= K_CONSTRAINT K_ON "(" Variable NodeLabel ")" K_ASSERT K_EXISTS "(" PropertyExpression ")"

CreateNodeKeyConstraint ::= K_CREATE NodeKeySyntax
DropNodeKeyConstraint ::= K_DROP NodeKeySyntax
NodeKeySyntax ::= K_CONSTRAINT K_ON "(" Variable NodeLabel ")" K_ASSERT "(" PropertyExpression ("," PropertyExpression)* ")" K_IS K_NODE K_KEY

CreateRelationshipPropertyExistenceConstraint ::= K_CREATE RelationshipPropertyExistenceConstraintSyntax
DropRelationshipPropertyExistenceConstraint ::= K_DROP RelationshipPropertyExistenceConstraintSyntax
RelationshipPropertyExistenceConstraintSyntax ::= K_CONSTRAINT K_ON RelationshipPatternSyntax K_ASSERT K_EXISTS "(" PropertyExpression ")"
RelationshipPatternSyntax ::= ("(" ")" "-" "[" Variable RelType "]" "-" "(" ")")
      | ("(" ")" "-" "[" Variable RelType "]" "-" ">" "(" ")")
      | ("(" ")" "<" "-" "[" Variable RelType "]" "-" "(" ")")
/*************************
 * Query
 */
Query ::= StandaloneCall | BulkImportQuery | RegularQuery

//-- RegularQuery
RegularQuery ::= SingleQuery Union*
Union ::= K_UNION K_ALL? SingleQuery
SingleQuery ::= MultiPartQuery | SinglePartQuery

SinglePartQuery ::= ((ReadingClause* UpdatingClause+ Return?) | ReadingWithReturn)

ReadingWithReturn ::= ReadingClause* Return {pin=1 recoverWhile=statement_recover}

MultiPartQuery ::= ((ReadingClause* UpdatingClause* With)+ SinglePartQuery)

UpdatingClause ::= Create
               | Merge
               | Foreach
               | Delete
               | SetClause
               | Remove

ReadingClause ::= LoadCSV
              | Start
              | Match
              | Unwind
              | InQueryCall

//-- BulkImportQuery
BulkImportQuery ::= PeriodicCommitHint LoadCSVQuery
PeriodicCommitHint ::= K_USING K_PERIODIC K_COMMIT IntegerLiteral?
LoadCSVQuery ::= LoadCSV SingleQuery?

/*************************
 * Clauses
 */
//-- Load csv
LoadCSV ::= K_LOAD K_CSV (K_WITH K_HEADERS)? K_FROM Expression K_AS Variable (K_FIELDTERMINATOR StringLiteral)?

//-- Start
Start ::= K_START (StartPoint ("," StartPoint)*)? Where?
StartPoint ::= Variable "=" Lookup
Lookup ::= NodeLookup | RelationshipLookup

NodeLookup ::= K_NODE (IdentifiedIndexLookup | IndexQuery | IdLookup)
RelationshipLookup ::= (K_RELATIONSHIP | K_REL) (IdentifiedIndexLookup | IndexQuery | IdLookup)

IdentifiedIndexLookup ::= ":" SymbolicNameString "(" SymbolicNameString "=" (StringLiteral | Parameter) ")"
IndexQuery ::= ":" SymbolicNameString "(" (StringLiteral | Parameter) ")"
IdLookup ::= "(" (LiteralIds | Parameter | "*") ")"
LiteralIds ::= IntegerLiteral ("," IntegerLiteral)*

//-- Match
Match ::= K_OPTIONAL? K_MATCH Pattern Hint* Where?
Hint ::= K_USING ((K_INDEX Variable NodeLabel "(" PropertyKeyName ")")
       | (K_JOIN K_ON Variable ("," Variable)*)
       | (K_SCAN Variable NodeLabel))

//-- Merge
Merge ::= K_MERGE PatternPart MergeAction*
MergeAction ::= K_ON (K_MATCH | K_CREATE) SetClause

//-- SetClause
SetClause ::= K_SET SetItem ("," SetItem)*
SetItem ::= (PropertyExpression "=" Expression)
          | (Variable "=" Expression)
          | (Variable "+=" Expression)
          | (Variable NodeLabels)

//-- Remove
Remove ::= K_REMOVE RemoveItem ("," RemoveItem)*
RemoveItem ::= (Variable NodeLabels) | PropertyExpression

//-- Return
With ::= K_WITH K_DISTINCT? ReturnBody Where?
Return ::= K_RETURN K_DISTINCT? ReturnBody
ReturnBody ::= ReturnItems Order? Skip? Limit?
ReturnItems ::= ("*" | ReturnItem) ("," ReturnItem)*
ReturnItem ::= (Expression K_AS Variable) | (Expression)
Order ::= K_ORDER K_BY SortItem ("," SortItem)*
SortItem ::= Expression (K_DESCENDING | K_DESC | K_ASCENDING | K_ASC)?
Skip ::= K_SKIP Expression
Limit ::= K_LIMIT Expression

//-- Call
Call ::= K_CALL ProcedureInvocation ProcedureResults?
InQueryCall ::= Call Where?
StandaloneCall ::= Call (Where? Return)?
ProcedureInvocation ::= ProcedureInvocationBody ProcedureArguments {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
ProcedureInvocationBody ::= Namespace ProcedureName
ProcedureArguments ::= "(" Expression? ("," Expression)* ")" {
   implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherArgumentList"
}
ProcedureResults ::= K_YIELD ProcedureResult ("," ProcedureResult)*
ProcedureResult ::= AliasedProcedureResult | SimpleProcedureResult
AliasedProcedureResult ::= ProcedureOutput K_AS Variable
SimpleProcedureResult ::= Variable
ProcedureOutput ::= SymbolicNameString

//-- Other
Create ::= K_CREATE K_UNIQUE? Pattern
Delete ::= K_DETACH? K_DELETE Expression ("," Expression)*
Unwind ::= K_UNWIND Expression K_AS Variable
Foreach ::= K_FOREACH "(" Variable K_IN Expression "|" UpdatingClause+ ")"
Where ::= K_WHERE Expression

/*************************
 * Patterns
 */
Pattern ::= PatternPart ("," PatternPart)*
PatternPart ::= (Variable "=")? AnonymousPatternPart
AnonymousPatternPart ::= ShortestPathPattern | PatternElement
ShortestPathPattern ::= ShortestPathFunctionInvocation | AllShortestPathsFunctionInvocation {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherTypePropagator"
}
ShortestPathFunctionInvocation ::= (K_SHORTESTPATH "(" PatternElement ")") {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherPathYielding"
}
AllShortestPathsFunctionInvocation ::= (K_ALLSHORTESTPATHS "(" PatternElement ")") {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherListYielding"
}
PatternElement ::= ("(" PatternElement ")") | (NodePattern PatternElementChain*)
PatternElementChain ::= RelationshipPattern NodePattern {pin=1}

RelationshipPattern ::= LeftArrowHead? Dash RelationshipDetail? Dash RightArrowHead? {pin=2}

RelationshipDetail ::= "[" Variable? "?"? RelationshipTypes? MaybeVariableLength? Properties? "]" {pin=1}
RelationshipTypes ::= ":" RelTypeName ("|" ":"? RelTypeName)* {pin=1}
MaybeVariableLength ::= ("*" RangeLiteral?)

NodePattern ::= "(" Variable? NodeLabels? Properties? ")" {pin=1}

RelationshipsPattern ::= NodePattern PatternElementChain+ {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherPathYielding"
}
Properties ::= MapLiteral | Parameter

/*************************
 * Expression
 */
Expression ::= Expression12 {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherTypePropagator"
}
private Expression12 ::= Expression11 (K_OR Expression11)*
private Expression11 ::= Expression10 (K_XOR Expression10)*
private Expression10 ::= Expression9 (K_AND Expression9)*
private Expression9 ::= (K_NOT Expression9) | Expression8
private Expression8 ::= Expression7 PartialComparisonExpression*
private PartialComparisonExpression ::= ("=" Expression7)
                                      | ("<>" Expression7)
                                      | ("!=" Expression7)
                                      | ("<" Expression7)
                                      | (">" Expression7)
                                      | ("<=" Expression7)
                                      | (">=" Expression7)
private Expression7 ::= Expression6 (("-" Expression6) | ("+" Expression6))*
private Expression6 ::= Expression5 (("*" Expression5) | ("/" Expression5) | ("%" Expression5))*
private Expression5 ::= Expression4 ("^" Expression4)*
private Expression4 ::= Expression3 | ("-" Expression4) | ("+" Expression4)
private Expression3 ::= Expression2 (
    ("[" Expression "]")
  | ("[" Expression? ".." Expression? "]")
  | ("=~" Expression2)
  | (K_IN Expression2)
  | (K_STARTS K_WITH  Expression2)
  | (K_ENDS K_WITH  Expression2)
  | (K_CONTAINS  Expression2)
  | (K_IS K_NULL)
  | (K_IS K_NOT K_NULL)
)*
private Expression2 ::= Expression1 (PropertyLookup | NodeLabels)*
private Expression1 ::= UnaryOperator
              | NumberLiteral
              | StringLiteral
              | Parameter
              | BooleanLiteral
              | NullLiteral
              | CaseExpression
              | MapLiteral
              | MapProjection
              | CountStar
              | ListComprehension
              | PatternComprehension
              | Array

              | FilterFunctionInvocation
              | ExtractFunctionInvocation
              | ReduceFunctionInvocation
              | AllFunctionInvocation
              | AnyFunctionInvocation
              | NoneFunctionInvocation
              | SingleFunctionInvocation
              | ExistsFunctionInvocation

              | ShortestPathPattern
              | RelationshipsPattern
              | ParenthesizedExpression
              | FunctionInvocation
              | Variable


UnaryOperator ::= ("-" | "+") NumberLiteral {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherTypePropagator"
}

Array ::= ("[" Expression? ("," Expression)* "]") {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherListYielding"
}
PropertyLookup ::= "." PropertyKeyName
ListComprehension ::= "[" FilterExpression ("|"  Expression)? "]" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherListYielding"
}
FilterExpression ::= IdInColl Where?
IdInColl ::= Variable K_IN Expression
ParenthesizedExpression ::= "(" Expression ")" {
    pin=1
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherParenthesized"
}
FunctionInvocation ::= FunctionInvocationBody (("(" "*" ")") | FunctionArguments) {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
FunctionArguments ::= "(" K_DISTINCT? Expression? ("," Expression)* ")" {
   implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherArgumentList"
}
FunctionInvocationBody ::= Namespace FunctionName
CaseExpression ::= K_CASE ((CaseAlternatives+) | (Expression CaseAlternatives+))
                   (K_ELSE Expression)?
                   K_END {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherAnyYielding"
}
CaseAlternatives ::= K_WHEN Expression K_THEN Expression
PropertyExpression ::= Expression1 (PropertyLookup)+

FilterFunctionInvocation ::= K_FILTER "(" FilterExpression ")" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
ExtractFunctionInvocation ::= K_EXTRACT "(" FilterExpression ("|" Expression)? ")" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
ReduceFunctionInvocation ::= K_REDUCE "(" Variable "=" Expression "," IdInColl "|" Expression ")" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
AllFunctionInvocation ::= K_ALL "(" FilterExpression ")" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
AnyFunctionInvocation ::= K_ANY "(" FilterExpression ")" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
NoneFunctionInvocation ::= K_NONE "(" FilterExpression ")" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
SingleFunctionInvocation ::= K_SINGLE "(" FilterExpression ")" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}
ExistsFunctionInvocation ::= K_EXISTS "(" Expression ")" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherInvocation"
    methods=[getFullName]
}

PatternComprehension ::= "[" RelationshipsPattern Where? "|" Expression "]" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherListYielding"
}

/*************************
 * Literals
 */
MapProjection ::= Variable "{" MapProjectionVariants? ("," MapProjectionVariants)* "}" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherMapYielding"
}
MapProjectionVariants ::= (LiteralEntry | PropertySelector | VariableSelector | AllPropertiesSelector)
LiteralEntry ::= PropertyKeyName ":" Expression
PropertySelector ::= "." Variable
VariableSelector ::= Variable
AllPropertiesSelector ::= "." "*"

/*************************
 * Literals
 */
Variable ::= SymbolicNameString {
    mixin="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherVariableElementImpl"
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherVariableElement"
    methods=[getName setName getNameIdentifier getReferences]
}
FunctionName ::= UnescapedSymbolicNameString | EscapedSymbolicNameString | K_COUNT
ProcedureName ::= SymbolicNameString
CountStar ::= (K_COUNT "(" "*" ")") {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherIntegerYielding"
}
Parameter ::= NewParameter | OldParameter {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherAnyYielding"
    methods=[getParameterName]
}
OldParameter ::= "{" (SymbolicNameString | UnsignedInteger) "}"
NewParameter ::= "$" (SymbolicNameString | UnsignedInteger)
NodeLabel ::= ":" LabelName
StringLiteral ::= l_string {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherStringYielding"
}
BooleanLiteral ::= K_TRUE | K_FALSE {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherBooleanYielding"
}
NullLiteral ::= K_NULL {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherNullYielding"
}
LabelName ::= SymbolicNameString {
    mixin="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherNamedElementImpl"
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherNamedElement"
    methods=[getName setName getNameIdentifier getReferences]
}
RelType ::= ":" RelTypeName
RelTypeName ::= SymbolicNameString {
    mixin="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherNamedElementImpl"
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherNamedElement"
    methods=[getName setName getNameIdentifier getReferences]
}
PropertyKeyName ::= SymbolicNameString {
    mixin="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherNamedElementImpl"
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.CypherNamedElement"
    methods=[getName setName getNameIdentifier getReferences]
}
PropertyKeyNames ::= PropertyKeyName ("," PropertyKeyName)*

RangeLiteral ::= (IntegerLiteral? ".." IntegerLiteral?) | (IntegerLiteral)
MapLiteral ::= "{" (PropertyKeyName ":" Expression)? ("," PropertyKeyName ":" Expression)* "}" {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherMapYielding"
}
NodeLabels ::= NodeLabel+

NumberLiteral ::= DoubleLiteral | IntegerLiteral {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherTypePropagator"
}

DoubleLiteral ::= UnsignedDouble {// todo: make real double here
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherFloatYielding"
}

IntegerLiteral ::= UnsignedInteger {
    implements="com.neueda.jetbrains.plugin.graphdb.language.cypher.references.types.CypherIntegerYielding"
}

/*************************
 * Base
 */
Namespace ::= (SymbolicNameString ".")*
SymbolicNameString ::= UnescapedSymbolicNameString
    | EscapedSymbolicNameString
    | ReservedWord

ReservedWord ::= K_MATCH
                | K_YIELD
                | K_XOR
                | K_WITH
                | K_WHERE
                | K_WHEN
                | K_USING
                | K_UNWIND
                | K_UNIQUE
                | K_UNION
                | K_TRUE
                | K_THEN
                | K_STARTS
                | K_START
                | K_SKIP
                | K_SINGLE
                | K_SHORTESTPATH
                | K_SET
                | K_SCAN
                | K_SCALAR
                | K_RETURN
                | K_REQUIRE
                | K_REMOVE
                | K_RELATIONSHIP
                | K_REL
                | K_REDUCE
                | K_PROFILE
                | K_PERIODIC
                | K_ORDER
                | K_OR
                | K_OPTIONAL
                | K_ON
                | K_OF
                | K_NULL
                | K_NOT
                | K_NONE
                | K_NODE
                | K_MERGE
                | K_MANDATORY
                | K_LOAD
                | K_LIMIT
                | K_JOIN
                | K_IS
                | K_INDEX
                | K_IN
                | K_HEADERS
                | K_FROM
                | K_FOREACH
                | K_FOR
                | K_FILTER
                | K_FIELDTERMINATOR
                | K_FALSE
                | K_EXTRACT
                | K_EXPLAIN
                | K_EXISTS
                | K_ENDS
                | K_END
                | K_ELSE
                | K_DROP
                | K_DO
                | K_DISTINCT
                | K_DETACH
                | K_DESCENDING
                | K_DESC
                | K_DELETE
                | K_CYPHER
                | K_CSV
                | K_CREATE
                | K_COUNT
                | K_CONTAINS
                | K_CONSTRAINT
                | K_COMMIT
                | K_CASE
                | K_CALL
                | K_BY
                | K_BEGIN
                | K_ASSERT
                | K_ASCENDING
                | K_ASC
                | K_AS
                | K_ANY
                | K_AND
                | K_ALLSHORTESTPATHS
                | K_ALL
                | K_ADD
                | K_KEY

EscapedSymbolicNameString ::= l_identifier_text
UnescapedSymbolicNameString ::= l_identifier
Dash ::= "-" // todo: migrate all variants
LeftArrowHead ::= "<" // todo: migrate all variants
RightArrowHead ::= ">" // todo: migrate all variants
UnsignedDouble ::= l_decimal
UnsignedInteger ::= l_integer
